class String {
    field int len;
    field int maxLen;
    field Array chars;

    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let len = 0;
        let maxLen = maxLength;
        let chars = Array.new(maxLength);
        return this;
    }

    method void dispose() {
        do chars.dispose();
        return;
    }

    method int length() {
        return len;
    }

    method char charAt(int j) {
        return chars[j];
    }

    method void setCharAt(int j, char c) {
        let chars[j] = c;
        return;
    }

    method String appendChar(char c) {
        if (maxLen > len) {
            let chars[len] = c;
            let len = len + 1;
        }
        return this;
    }

    method void eraseLastChar() {
        if (len > 0) {
            let len = len - 1;
        }
        return;
    }

    method int intValue() {
        var int v, index, d, temp;
        var boolean isNeg;
        let v = 0;
        let index = 0;

        if (chars[index] = 45) {
            let index = index + 1;
            let isNeg = true;
        } else {
            let isNeg = false;
        }
        while (index < len) {
            let temp = chars[index];
            if ((temp < 48) | (temp > 57)) {
                if (isNeg) {
                    return -v;
                } else {
                    return v;
                }
            }
            let d = String.c2d(temp);
            let index = index + 1;
            let v = v * 10 + d;
        }

        if (isNeg) {
            return -v;
        } else {
            return v;
        }
    }

    method void setInt(int n) {
        let len = 0;
        if (n < 0) {
            let n = -n;
            do appendChar(45);
        }

        do setInt2(n);
        return;
    }

    method void setInt2(int n) {
        var int nextN;

        if (n < 10) {
            do appendChar(String.d2c(n));
        } else {
            let nextN = n / 10;
            do setInt2(nextN);
            do appendChar(String.d2c(n - (nextN * 10)));
        }

        return;
    }

    function char newLine() {
        return 128;
    }

    function char backSpace() {
        return 129;
    }

    function char doubleQuote() {
        return 34;
    }

    function int c2d(char c) {
        return c - 48;
    }

    function char d2c(int d) {
        return d + 48;
    }
}
